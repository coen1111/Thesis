! Square base shell
! Loading: pressure on the base shell
! BC: The edges of the plate are simply supported

!0. Initialise
! set geometrical parameters

!  lengths, width, and height of base shell, part 1 in xy-plane
L_1 = 0.0254						! [m] length of base shell 1 (xy-plane)
W_1 = L_1						    ! [m] width of base shell 1 (xy-plane)
H_1 = 254e-6						! [m] thickness of base shell 1 (xz and yz-plane)

! set material properties
E = 117.21e9                  		! [Pa] E-modulus of aluminium
v = 0.3								! [-] Poission ratio

!0.1 Set properties under investigation
! CHANGE THIS SECTION PER LOADCASE
q = 6894.75729						! [Pa=N/m^2], Pressure on base shell

!0.2 Set mesh properties
NDIV_Int_Y =1						! [#], Scaling for the mesh density, adjust from 1, 2, 3, ... 
NDIV_Y = NDIV_Int_Y*50					! [#], Number of divisions for the edge in y-direction 
NDIV_X = NDIV_Y						! [#], Number of divisions for the edge in x-direction 

!1. Preprocessor steps
! call preprocessor
/PREP7

!1.1 define material
MPTEMP,1,0
MPDATA,EX,1,,E
MPDATA,PRXY,1,,v

!1.2 select element type
! Element for the membrane of the base-shell
! From the documentation: For the stiffened shell, the most effective choice is to use KEYOPT(3) = 0 for the shell and KEYOPT(3) = 2 for the stiffener.
ET,1,SHELL181
KEYOPT, 1, 3, 0 			! KEYOPT, ITYPE, KNUM, VALUE
! Element for the bending behaviour of the stiffener
ET,2,SHELL181
KEYOPT, 2, 3, 2				! KEYOPT, ITYPE, KNUM, VALUE

!1.2 define element sections
!  section number 1 for the base shell, thickness (xz and yz-plane), width (z-plane) 
SECTYPE,1,SHELL					! SECTYPE, SectionID, type
SECDATA, H_1, 1,,, 				! SECDATA, thickness, material ID, theta (angle w.r.t. coordinate system), number of integration points

!1.3 define key points
! Keypoint [K], Keypoint number, X-coordinate, Y-coordinate, Z-coordinate
K, 			    1, 		  		0,				0,		 	 0
K, 				2,  	    	W_1,			0,			 0
K, 				3,  	    	W_1,			L_1,  	 	 0
K, 				4, 		  		0,			   	L_1, 	     0

!1.4 define lines with ID's
! Define Lines, index on sections
! Line [L], Keypoint number 1, Keypoint number 2
! Lines parallel to the x-axis
*GET, Line_ID1, LINE, 0, NUM, MAXD 			! Get the current total number of lines and save
L, 1, 2			! Line 1 base_shell
L, 3, 4			! Line 2 base_shell

! Lines parallel to the y-axis
*GET, Line_ID2, LINE, 0, NUM, MAXD 			! Get the current total number of lines and save
L, 2, 3			! Line 3 base_shell
L, 4, 1			! Line 4 base_shell

*GET, Line_ID3, LINE, 0, NUM, MAXD			! Get the current total number of lines and save

!1.4 define areas
! Define Areas, index on sections
! Area [A], Keypoint number 1, Keypoint number 2, Keypoint number 3, Keypoint number 4
*GET, Area_ID1, AREA, 0, NUM, MAXD			! Get the current total number of areas and save
A, 1, 2, 3, 4 	! Area 1 base_shell		

*GET, Area_ID2, AREA, 0, NUM, MAXD			! Get the current total number of areas and save

!1.5 set mesh density
! lines parallel to x-axis with Line_ID1
LSEL, S, LINE, , Line_ID1+1, Line_ID2, 1 	! select lines 
LESIZE, ALL, , , NDIV_X 					! mesh NDIV_X elements per line
LSEL, ALL								 	! deselect lines 

! lines parallel to y-axis with Line_ID2
LSEL, S, LINE, , Line_ID2+1, Line_ID3, 1 	! select lines
LESIZE, ALL, , , NDIV_Y			 			! mesh NDIV_Y elements per line
LSEL, ALL									 ! deselect lines 

!1.6 mesh
TYPE, 1
SECNUM, 1
ASEL, S, AREA, , Area_ID1+1, AREA_ID2		! select areas
AMESH, ALL
ASEL, ALL								 	! deselect areas

!Get the total number of elements
*GET, TotNumElem, elem, , count 

!1.7 Set constraints
! Simply supported lines parallel to x-axis with Line_ID1 
! Constraint them in y direction
LSEL, S, LINE, , Line_ID1+1, Line_ID2, 1 	! select lines
DL, ALL, , UY, 0, 
LSEL, ALL									! deselect lines
! Constraint them in z direction
LSEL, S, LINE, , Line_ID1+1, Line_ID2, 1 	! select lines 
DL, ALL, , UZ, 0, 
LSEL, ALL									! deselect lines

! Simply support lines parallel to y-axis with Line_ID2,
! Constraint them in y direction
LSEL, S, LINE, , Line_ID2+1, Line_ID3, 1 	! select lines 
DL, ALL, , UX, 0, 
LSEL, ALL									! deselect lines
! Constraint them in z direction
LSEL, S, LINE, , Line_ID2+1, Line_ID3, 1 	! select lines
DL, ALL, , UZ, 0, 
LSEL, ALL									! deselect lines

!1.8 Set loadings
! Apply a pressure on the base shell with AREA_ID1
ASEL, S, AREA, , Area_ID1+1, AREA_ID2 		! select areas
SFA, ALL, , PRES, q, 
ASEL, ALL									! deselect areas

!1.9 Save image of setup
/RGB,INDEX,100,100,100, 0   
/RGB,INDEX, 80, 80, 80,13   
/RGB,INDEX, 60, 60, 60,14   
/RGB,INDEX, 0, 0, 0,15  
APLOT 
/VIEW,1,1,2,3   
/ANG,1  
/REP,FAST   
!*  
/PSF,PRES,NORM,2,0,1
/PBF,DEFA, ,1   
/PSYMB,CS,0 
/PSYMB,NDIR,0   
/PSYMB,ESYS,0   
/PSYMB,LDIV,0   
/PSYMB,LDIR,0   
/PSYMB,ADIR,0   
/PSYMB,ECON,0   
/PSYMB,XNODE,0  
/PSYMB,DOT,1
/PSYMB,PCONV,   
/PSYMB,LAYR,0   
/PSYMB,FBCS,0   
!*  
/PBC,ALL, ,1
/REP
/IMAGE, SAVE, TotNumElem_%TotNumElem%_setup, eps,
/IMAGE, SAVE, TotNumElem_%TotNumElem%_setup, png,

!2. Define solutions and solve
/SOLU
SOLVE

!3. Post processing
! Enter post processor
/POST1

! Get the displacement in z-direction of the center
NodeNumCenter = Node(W_1/2, L_1/2, 0)
*GET, w_center, NODE, NodeNumCenter, U, Z

!3.1 Save the graphical output
! Open Results viewer
/SHOW,WIN32C
SET,FIRST   
/PLOPTS,INFO,3  
/PNUM,MAT,1 
/NUMBER,1   
/REPLOT,RESIZE  

! Set to displacement in z-direction
PLNSOL,U,Z

! Set view to 3D and save as eps and png
/VIEW,1,1,1,1   
/ANG,1
/REP,FAST  
/IMAGE, SAVE, TotNumElem_%TotNumElem%_displacements_z_3dview, eps,
/IMAGE, SAVE, TotNumElem_%TotNumElem%_displacements_z_3dview, png,

! Set view to sideview of xz plane and save
/VIEW,1,,-1 
/ANG,1  
/REP,FAST
/IMAGE, SAVE, TotNumElem_%TotNumElem%_displacements_z_sideview_xz, eps,
/IMAGE, SAVE, TotNumElem_%TotNumElem%_displacements_z_sideview_xz, png,

! Set view to sideview of yz plane and save
/VIEW,1,1   
/ANG,1,-90,ZS,1 
/REP,FAST 
/IMAGE, SAVE, TotNumElem_%TotNumElem%_displacements_z_sideview_yz, eps,
/IMAGE, SAVE, TotNumElem_%TotNumElem%_displacements_z_sideview_yz, png,

! Set view to topview of yx plane and save
/VIEW,1,,,1 
/ANG,1  
/REP,FAST
/IMAGE, SAVE, TotNumElem_%TotNumElem%_displacements_z_topview_xy, eps,
/IMAGE, SAVE, TotNumElem_%TotNumElem%_displacements_z_topview_xy, png,

!3.1 Save the parameters output
PARSAV, SCALAR, TotNumElem_%TotNumElem%_center_displacement, txt,
